
buildscript {
  repositories {
    maven {
      url "http://dl.bintray.com/ajmath/gradle-plugins"
    }
  }
  dependencies {
    classpath "com.ajmath:gradle-s3crets:0.1.2"
  }
}

plugins {
    id "com.jfrog.bintray" version "1.1"
}

apply plugin: "com.ajmath.gradle-s3crets"
apply plugin: 'groovy'
apply plugin: 'maven'

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile 'com.amazonaws:aws-java-sdk-s3:1.10.1'
}

version = "0.2.0"
group = 'com.ajmath'

s3crets {
  properties "s3://secrets-ajmath/gradle/bintray-publish.properties"
}

configurations {
  archives
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives jar, sourceJar
}

bintray {
  user = 'ajmath'
  key = "${bintray_apikey}"

  configurations = [ 'archives' ]

  dryRun = false //Whether to run this as dry-run, without deploying
  publish = true //If version should be auto published after an upload
  pkg {
    repo = 'gradle-plugins'
    name = 'gradle-s3crets'
    desc = 'Pull senstive gradle properties from s3'
    websiteUrl = 'https://github.com/ajmath/gradle-s3crets'
    issueTrackerUrl = 'https://github.com/ajmath/gradle-s3crets/issues'
    vcsUrl = 'https://github.com/ajmath/gradle-s3crets.git'
    licenses = ['WTFPL']
    labels = ['gradle', 's3', 'secrets']
    publicDownloadNumbers = true
    version {
      name = project.version //Bintray logical version name
      released  = new Date()
      vcsTag = project.version
      attributes = [
        'gradle-plugin': "com.ajmath.gradle-s3crets:${project.group}:${project.name}"
      ]
      //Optional configuration for GPG signing
      gpg {
        sign = true //Determines whether to GPG sign the files. The default is false
        passphrase = "${bintray_gpgpassphrase}" //Optional. The passphrase for GPG signing'
      }
    }
  }
}
